allprojects {
    repositories {
        google()
        mavenCentral()
    }
}
subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            project.android {
                // 没有命名空间的插件，添加命名空间
                if (namespace == null) {
                    namespace project.group
                    project.logger.error(
                            "Flutter plugin: [" + project.name + "] "
                                    + "need to add the namespace "
                                    + project.group
                    )
                }

                // 对编译SDK小于31的强制设置为34
                Integer pluginCompileSdk = project.android.compileSdk
                if (pluginCompileSdk != null && pluginCompileSdk < 31) {
                    compileSdk 34
                    project.logger.error(
                            "Flutter plugin: [" + project.name + "] "
                                    + "Overriding compileSdk version from "
                                    + pluginCompileSdk
                                    + " to 34"
                    )
                }
            }
        }
    }
}
rootProject.buildDir = "../build"
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(":app")
}
subprojects {
    buildscript {
        ext.kotlin_version = '1.8.22'
        dependencies {
            classpath 'com.android.tools.build:gradle:8.1.4'
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        }
    }
}
tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
